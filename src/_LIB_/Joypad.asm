; =========================================================================================================================================================
; Mighty The Armadillo in PRISM PARADISE
; By Nat The Porcupine 2021
; ---------------------------------------------------------------------------------------------------------------------------------------------------------
; Joypad functions
; =========================================================================================================================================================
; ---------------------------------------------------------------------------------------------------------------------------------------------------------
; Read joypad input
; ---------------------------------------------------------------------------------------------------------------------------------------------------------
; PARAMETERS:
;	Nothing
; ---------------------------------------------------------------------------------------------------------------------------------------------------------
; RETURNS:
;	Nothing
; ---------------------------------------------------------------------------------------------------------------------------------------------------------
ReadJoypads:
		lea	rP1Data.w,a0			; Controller data in RAM
		lea	PORT_A_DATA,a1			; Port 1
		bsr.s	ReadJoypad			; Read the joypad data
		addq.w	#rP2Data-rP1Data,a1		; Port 2

ReadJoypad:
		move.b	#0,(a1)				; Set TH = 0
		nop					; Wait
		nop					; ''
		move.b	(a1),d0				; Input data when TH = 0
		asl.b	#2,d0				; Chane data when TH = 0 & wait
		move.b	#$40,(a1)			; Set TH = 1
		andi.b	#$C0,d0				; Chane data when TH = 0 & wait
		move.b	(a1),d1				; Input data when TH = 1
		andi.b	#$3F,d1
		or.b	d1,d0	
		not.b	d0				; Push -> 1
		move.b	(a0),d1				; Copy
		eor.b	d0,d1
		move.b	d0,(a0)+			; Switch data
		and.b	d0,d1			
		move.b	d1,(a0)+			; Switch edge data
		rts
; =========================================================================================================================================================